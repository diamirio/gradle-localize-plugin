plugins {
    id("java-gradle-plugin")
    id("maven-publish")
    alias(libs.plugins.benmanes.versions)
    alias(libs.plugins.gradle.plugin.publish)
    alias(libs.plugins.kotlin.jvm)
    alias(libs.plugins.kotlin.serialization)
    alias(libs.plugins.kotlinter)
}

apply plugin: 'maven-publish'

group 'com.tailoredapps.gradle'
version '0.4.0'

// We need to keep compatibility to Java 8 here, as otherwise projects on some machines targeting Java 11 will not be
// able to load the plugin.
targetCompatibility = 1.8
sourceCompatibility = 1.8

def ext_displayName = 'Gradle Localize Plugin'
def ext_description = 'Gradle Plugin to generate Android string resource files from a localization spreadsheet.'
def ext_website = 'https://github.com/tailoredmedia/gradle-localize-plugin'
def ext_vcs = 'https://github.com/tailoredmedia/gradle-localize-plugin.git'
def ext_labels = ['gradle', 'android', 'localization']

repositories {
    mavenCentral()
}

dependencies {
    implementation libs.diffutils
    implementation libs.google.api.client
    implementation libs.google.sheets
    implementation libs.kotlinx.coroutines.jdk8
    implementation libs.kotlinx.coroutines.core
    implementation libs.kotlinx.serialization.json

    testImplementation libs.junit
    testImplementation libs.kluent
    testImplementation libs.kotlinx.coroutines.test
    testImplementation libs.mockk
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}


gradlePlugin {
    website = ext_website
    vcsUrl = ext_vcs
    plugins {
        simplePlugin {
            id = 'com.tailoredapps.gradle.localize'
            description = ext_description
            tags.addAll(ext_labels)
            implementationClass = 'com.tailoredapps.gradle.localize.GradleLocalizePlugin'
        }
    }
}

task sourceJar(type: Jar) {
    description = "An archive of the source code for Maven Central"
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}


Properties properties = new Properties()
def credentialPropertiesFile = ""
def injectedCredentialsFilePath = System.getenv('CREDENTIALS_PROPERTIES_FILE')
if (injectedCredentialsFilePath != null && !injectedCredentialsFilePath.isEmpty()) {
    credentialPropertiesFile = injectedCredentialsFilePath
} else {
    credentialPropertiesFile = 'local.properties'
}
try {
    properties.load(project.rootProject.file(credentialPropertiesFile).newDataInputStream())
} catch (IOException e) {
    println("'${credentialPropertiesFile}' not found. This can be ignored, unless you want to publish. " + e.localizedMessage)
}


// Configure the maven-publish plugin
publishing {
    publications {
        localizePlugin(MavenPublication) {
            from components.java
            pom {
                name = ext_displayName
                description = ext_description
                url = ext_website
                packaging = 'jar'
                licenses {
                    license {
                        name = 'Apache-2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'tailoredmedia'
                        name = 'Tailored Apps'
                        email = 'office@tailored-apps.com'
                    }
                }
                scm {
                    connection = ext_vcs
                    developerConnection = ext_vcs
                    url = ext_website
                }
            }
        }
    }

    repositories {
        maven {
            url 'https://maven.tailored-apps.com/repository/maven-public'
            credentials {
                username properties.getProperty("maven.publish.username")
                password properties.getProperty("maven.publish.password")
            }
        }
    }
}
